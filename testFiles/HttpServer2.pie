//PIE HTTP SERVER EXAMPLE
import http;
import json;


//middleware
string accessToken="12345678";

//error sending
void sendError(ptr response, int status, string message, string error) {
    http.response_set_status(response, status);
    ptr jsonResponse = json.create_object();
    json.set_string(jsonResponse, "message", message);
    json.set_string(jsonResponse, "Error", error);
    http.response_set_header(response, "Content-Type", "application/json");
    http.response_set_body(response, json.stringify(jsonResponse));
}


bool checkAuth(string authHeader){
	//check if the header is a bearer type
	if(string_contains(authHeader,"Bearer")==1){
		//extract the token
		int tokenStart=string_index_of(authHeader," ");
		string token=string_substring(authHeader,tokenStart,strlen(authHeader));
		token=string_trim(token);
		output("Token: "+token,string);
		if(accessToken==token){
			return true;
		}
		return false;
	}else{
		output("Wrong authorization type",string);
		return false;
	}
}


bool authenticate(ptr request,ptr response){
	dict requestHeaders=http.request_get_headers(request);
	if(dict_has_key(requestHeaders,"Authorization")!=1){
	output_string("Unauthorized request");
		sendError(response,400,"Bad Request","Missing Authorization Header");
		return false;
	}else{
		output("Request Auth Header received! ",string);
		string authHeader=dict_get(requestHeaders,"Authorization");
		bool authCheck=checkAuth(authHeader);
		if(authCheck==false){
			sendError(response,403,"Forbidden","Invalid or expired token");
			return false;
		}else{
			return authCheck;
		}
	}
}


//Utilities
ptr getDBData(){
	file database=file_open("users.json","r");
	string data="";
	if (database == null) {
	    data="";
	}
	else{
		data=file_read_all(database);
		//output_string("Database: "+data);
	}
	file_close(database);
	if(string_is_empty(data)==1){
	output("Database is empty",string);
		return json.parse("[]");
	}else{
	return json.parse(data);
	}
}

void recordUser(ptr user){
	output("Recording user",string);
	// Read existing data BEFORE opening file in write mode
	ptr dbData=getDBData();
	json.array_push_object(dbData,user);
	
	// Now open file in write mode and save the updated data
	file database=file_open("users.json","w");
	file_write(database,json.stringify(dbData));
	file_flush(database);
	file_close(database);
}

//Controllers
void addUser(ptr request,ptr response){
	output_string("Adding user");
	//Process the request and see if the user exists
	//get the request body
	string body=http.request_get_body(request);
	output("Body received: "+body,string);
	//parse the json to get the user payload
	ptr user=json.parse(body);
	string userMail=json.get_string(user,"email");
	string userName=json.get_string(user,"name");
	if(string_is_empty(userMail)==1||string_is_empty(userName)==1){
		output("Invalid Body Detected!",string);
		sendError(response,400,"Bad Request","Invalid or missing user data");
		return;
	}else{
		//to record the data
		ptr userObj=json.create_object();
		json.set_string(userObj,"name",userName);
		json.set_string(userObj,"email",userMail);
		recordUser(userObj); //record to database
		http.response_set_status(response,201);
		ptr jsonResponse=json.create_object();
		json.set_string(jsonResponse,"message","User Added successfully");
		json.set_string(jsonResponse,"success","true");
		json.set_string(jsonResponse,"Error","Null");
		http.response_set_header(response, "Content-Type", "application/json");
		http.response_set_body(response,json.stringify(jsonResponse));
		return;
	}
}

//router to call the relevant functions for the relevant methods
void router(string method,string path,ptr request,ptr response){
	//authenticate all post requests
	if(method=="POST"){
		bool securityCheck=authenticate(request,response);
		if(securityCheck==false){
			return;
		}
	}
	if(method=="POST" && path=="/users"){
		addUser(request,response);
		return;
	}
	//Catch all
	http.response_set_header(response, "Content-Type", "text/plain");
	http.response_set_status(response, 404);
	http.response_set_body(response,("Route ["+method+" "+path+"] Not found"));
}

//main handler
void pieServer(ptr request, ptr response){
   //getting the route 
   string path=http.request_get_path(request);
   //getting the method
   string method=http.request_get_method(request);
   output_string(method + "Request Received" + "Path: "+path);
   router(method,path,request,response); 
}




int PORT=8000;

http.listen(PORT,pieServer);
