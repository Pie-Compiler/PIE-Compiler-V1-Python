//PIE HTTP SERVER EXAMPLE
import http;
import json;


//middleware
string accessToken="12345678";

int checkAuth(string authHeader){
	//check if the header is a bearer type
	if(string_contains(authHeader,"Bearer")==1){
		//extract the token
		int tokenStart=string_index_of(authHeader," ");
		string token=string_substring(authHeader,tokenStart,strlen(authHeader));
		token=string_trim(token);
		output("Token: "+token,string);
		if(accessToken==token){
			return 1;
		}
		return 0;
	}else{
		output("Wrong authorization type",string);
		return 0;
	}
}


int authenticate(ptr request,ptr response){
	dict requestHeaders=http.request_get_headers(request);
	if(dict_has_key(requestHeaders,"Authorization")!=1){
	output_string("Unauthorized request");
		http.response_set_status(response,401); //set unauthorized header

		ptr jsonResponse=json.create_object();
		json.set_string(jsonResponse,"message","Unauthorized");
		json.set_string(jsonResponse,"Error","Missing authorization header");
		http.response_set_header(response, "Content-Type", "application/json");
		http.response_set_body(response,json.stringify(jsonResponse));

		return 0;
	}else{
		output("Request Auth Header received! ",string);
		string authHeader=dict_get(requestHeaders,"Authorization");
		int authCheck=checkAuth(authHeader);
		if(authCheck==0){
			http.response_set_status(response,401); //set unauthorized header
			ptr jsonResponse=json.create_object();
			json.set_string(jsonResponse,"message","Unauthorized");
			json.set_string(jsonResponse,"Error","Invalid Token");
			http.response_set_header(response, "Content-Type", "application/json");
			http.response_set_body(response,json.stringify(jsonResponse));
			return 0;
		}else{
			return authCheck;
		}
	}
}


//Utilities
ptr getDBData(){
	file database=file_open("users.json","r");
	string data="";
	if (database == null) {
	    data="";
	}
	else{
		data=file_read_all(database);
		//output_string("Database: "+data);
	}
	file_close(database);
	if(string_is_empty(data)==1){
	output("Database is empty",string);
		return json.parse("[]");
	}else{
	return json.parse(data);
	}
}

void recordUser(ptr user){
	output("Recording user",string);
	// Read existing data BEFORE opening file in write mode
	ptr dbData=getDBData();
	json.array_push_object(dbData,user);
	
	// Now open file in write mode and save the updated data
	file database=file_open("users.json","w");
	file_write(database,json.stringify(dbData));
	file_flush(database);
	file_close(database);
}

//Controllers
void addUser(ptr request,ptr response){
	output_string("Adding user");
	//Process the request and see if the user exists
	//get the request body
	string body=http.request_get_body(request);
	output("Body received: "+body,string);
	//parse the json to get the user payload
	ptr user=json.parse(body);
	string userMail=json.get_string(user,"email");
	string userName=json.get_string(user,"name");
	if(string_is_empty(userMail)==1||string_is_empty(userName)==1){
		output("Invalid Body Detected!",string);
		http.response_set_status(response,400); //set unauthorized header
		ptr jsonResponse=json.create_object();
		json.set_string(jsonResponse,"message","Invalid Request Data");
		json.set_string(jsonResponse,"Error","Missing email and name");
		http.response_set_header(response, "Content-Type", "application/json");
		http.response_set_body(response,json.stringify(jsonResponse));
		return;
	}else{
		//to record the data
		ptr userObj=json.create_object();
		json.set_string(userObj,"name",userName);
		json.set_string(userObj,"email",userMail);
		recordUser(userObj); //record to database
		http.response_set_status(response,201);
		ptr jsonResponse=json.create_object();
		json.set_string(jsonResponse,"message","User Added successfully");
		json.set_string(jsonResponse,"success","true");
		json.set_string(jsonResponse,"Error","Null");
		http.response_set_header(response, "Content-Type", "application/json");
		http.response_set_body(response,json.stringify(jsonResponse));
		return;
	}
}

//router to call the relevant functions for the relevant methods
void router(string method,string path,ptr request,ptr response){
	//authenticate all post requests
	if(method=="POST"){
		int securityCheck=authenticate(request,response);
		if(securityCheck==0){
			return;
		}
	}
	if(method=="POST" && path=="/users"){
		addUser(request,response);
		return;
	}
	//Catch all
	http.response_set_header(response, "Content-Type", "text/plain");
	http.response_set_status(response, 404);
	http.response_set_body(response,("Route ["+method+" "+path+"] Not found"));
}

//main handler
void pieServer(ptr request, ptr response){
   //getting the route 
   string path=http.request_get_path(request);
   //getting the method
   string method=http.request_get_method(request);
   output_string(method + "Request Received" + "Path: "+path);
   router(method,path,request,response); 
}




int PORT=8000;

http.listen(PORT,pieServer);
